PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX example: <http://example/ns/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX stix: <http://docs.oasis-open.org/cti/ns/stix#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
											 
	?object_iri a ?stixType ;
	stix:id ?id;
	stix:type ?type;
	
	stix:alias ?alias;
#	stix:contact_information ?contact_information_string;
	stix:created ?dt_created;
	stix:description ?description;
	stix:first_seen ?dt_first_seen;
	stix:goals ?goal_list;
	stix:identity_class ?identity_class;
	stix:indicator_types ?indicator_list;
	stix:kill_chain_phases ?chains;
	stix:malware_types ?malware_type;
	stix:modified ?dt_modified;
	stix:name ?name;
	stix:objective ?objective;
	stix:resource_level ?resource_level;
	stix:pattern_type ?pattern_type;
	stix:pattern ?pattern;
	stix:primary_motivation ?primary_motivation;
	stix:relationship_type ?relationship_type;
	stix:roles ?role_list;
	stix:secondary_motivations ?secondary_motivation_list;
	stix:sectors ?sector_list;
	stix:sophistication ?sophistication;
	stix:source_ref ?source_ref_iri;
	stix:spec_version ?spec_version;
	stix:target_ref ?target_ref_iri;
	stix:tool_types ?tool_types_list;
	stix:valid_from ?dt_valid_from;
	.

# ==========================

	stix:id a owl:DatatypeProperty .
	stix:type a owl:DatatypeProperty .
	
	stix:alias a owl:DatatypeProperty .
#	stix:contact_information a owl:DatatypeProperty .
	stix:created a owl:DatatypeProperty .
	stix:description a owl:DatatypeProperty .
	stix:first_seen a owl:DatatypeProperty .

	stix:identity_class a owl:DatatypeProperty .
	stix:kill_chain_phases a owl:DatatypeProperty .

	stix:indicator_types a owl:DatatypeProperty .
	stix:malware_types a owl:DatatypeProperty .
	stix:modified a owl:DatatypeProperty .
	stix:name a owl:DatatypeProperty .
	stix:objective a owl:DatatypeProperty .

	stix:resource_level a owl:DatatypeProperty .

	stix:pattern_type a owl:DatatypeProperty .
	stix:pattern a owl:DatatypeProperty .
	stix:primary_motivation a owl:DatatypeProperty .
	stix:relationship_type a owl:DatatypeProperty .

	stix:roles a owl:DatatypeProperty .
	stix:goals a owl:DatatypeProperty .
	stix:secondary_motivations a owl:DatatypeProperty .
	stix:sectors a owl:DatatypeProperty .
	stix:sophistication a owl:DatatypeProperty .

	stix:source_ref a owl:ObjectProperty .
	stix:spec_version a owl:DatatypeProperty .
	stix:target_ref a owl:ObjectProperty .
	
	# explicitly typing uses and indicates as ObjectProperties
	stix:uses a owl:ObjectProperty .
	stix:indicates a owl:ObjectProperty .
	?source_ref_iri ?relation_iri ?target_ref_iri .

	stix:tool_types a owl:DatatypeProperty .
	stix:valid_from a owl:DatatypeProperty .

	stix:external_reference a owl:ObjectProperty .
	stix:source_name a owl:DatatypeProperty .
	stix:url a owl:DatatypeProperty .
	stix:description a owl:DatatypeProperty .
	stix:external-id a owl:DatatypeProperty .
	
	stix:kill_chain_phase a owl:ObjectProperty .
	stix:kill_chain_name a owl:DatatypeProperty .
	stix:kill_chain_phase_name a owl:DatatypeProperty .
	
# ==========================


	?object_iri stix:external_reference ?exref_iri .
	?exref_iri a stix:StixObject .
	?exref_iri stix:source_name ?ex_ref_source_name .
	?exref_iri stix:url ?ex_ref_url .
	?exref_iri stix:description ?ex_ref_description .
	?exref_iri stix:external-id ?ex_ref_external_id .
	
	?object_iri stix:kill_chain_phase ?kill_chain_phase_iri .
	?kill_chain_phase_iri a stix:StixObject .
	?kill_chain_phase_iri stix:kill_chain_name ?kill_name .
	?kill_chain_phase_iri stix:kill_chain_phase_name ?phase_name .	

		

}
WHERE {
	SERVICE <x-sparql-anything:blank-nodes=false> {
		fx:properties fx:location "input/apt1.json" .


	# root array of objects
			?root xyz:objects ?objects .
	
	# individual objects from the objects array
			?objects ?object_slot ?object .
	
	# the type and id of the object 
			?object xyz:type ?type .
			?object xyz:id ?id .


	### OPTIONAL ###
	# aliases
			OPTIONAL {
				?object xyz:aliases ?aliases . 
				?aliases fx:anySlot ?alias .
			}

	# contact_information
			OPTIONAL {?object xyz:contact_information ?contact_information .}
			BIND(xsd:string(?contact_information) AS ?contact_information_string )

	# created
			OPTIONAL {?object xyz:created ?created . }

	# description
			OPTIONAL {?object xyz:description ?description . }

	# external_references
			OPTIONAL {
				?object xyz:external_references ?external_references . 
				?external_references fx:anySlot ?external_reference .
				?external_reference xyz:source_name ?ex_ref_source_name .
				OPTIONAL { ?external_reference xyz:description ?ex_ref_description . }
				OPTIONAL { ?external_reference xyz:external_id ?ex_ref_external_id . }
				OPTIONAL { ?external_reference xyz:url ?ex_ref_url . }
				BIND (IRI(CONCAT("http://docs.oasis-open.org/cti/ns/stix#ExternalReference-", STRUUID() )) AS ?exref_iri ) .				
			}

	# first_seen
			OPTIONAL {?object xyz:first_seen ?first_seen . }

	# identity_class
			OPTIONAL {?object xyz:identity_class ?identity_class .}

	# indicator_types
			OPTIONAL {?object xyz:indicator_types ?indicator_types . 
						?indicator_types ?indicator_slot ?indicator_list .	}

	# kill_chain_phases
			OPTIONAL {
				?object xyz:kill_chain_phases ?kill_chain_phases . 
				?kill_chain_phases ?anySlot ?chain_list .
				?chain_list xyz:kill_chain_name ?kill_name .
				OPTIONAL { ?chain_list 	xyz:phase_name ?phase_name . }
				BIND (IRI(CONCAT("http://docs.oasis-open.org/cti/ns/stix#KillChainPhase-", STRUUID() )) AS ?kill_chain_phase_iri ) .
			}

	# malware_types
			OPTIONAL {
				?object xyz:malware_types ?malware_types . 
				?malware_types  ?malware_types_slot ?malware_type .
			}

	# modified
			OPTIONAL {?object xyz:modified ?modified . }

	# name
			OPTIONAL {?object xyz:name ?name . }

	# objective
			OPTIONAL {?object xyz:objective ?objective . }

	# pattern
			OPTIONAL {?object xyz:pattern ?pattern . }

	# pattern_type
			OPTIONAL {?object xyz:pattern_type ?pattern_type . }

	# primary_motivation
			OPTIONAL {?object xyz:primary_motivation ?primary_motivation . }

	# relationship_type
			OPTIONAL {?object xyz:relationship_type ?relationship_type . }

	# resource_level
			OPTIONAL {?object xyz:resource_level ?resource_level . }

	# roles
			OPTIONAL {
				?object xyz:roles ?roles . 
				?roles ?roles_slot ?role_list .	
			}

	# goals
			OPTIONAL {
				?object xyz:goals ?goals . 
				?goals ?goals_slot ?goal_list .	
			}

	# secondary_motivations
			OPTIONAL {
				?object xyz:secondary_motivations ?secondary_motivations . 
				?secondary_motivations ?secondary_motivations_slot ?secondary_motivation_list .	
			}

	# sectors
			OPTIONAL {?object xyz:sectors ?sectors . 
				?sectors ?sectors_slot ?sector_list .	}

	# name
			OPTIONAL {?object xyz:sophistication ?sophistication . }

	# source_ref
			OPTIONAL {?object xyz:source_ref ?source_ref . }

	# spec_version
			OPTIONAL {?object xyz:spec_version ?spec_version . }

	# target_ref
			OPTIONAL {?object xyz:target_ref ?target_ref . }
		
	# tool_types
			OPTIONAL {?object xyz:tool_types ?tool_types . 
						?tool_types ?tool_types_slot ?tool_types_list .	}

	# valid_from
			OPTIONAL {?object xyz:valid_from ?valid_from . }

			}

		
	# Reformat dates to allow ingestion into xsd:dateTime
	BIND(xsd:dateTime(?created) AS ?dt_created )
	BIND(xsd:dateTime(?modified) AS ?dt_modified )
	BIND(xsd:dateTime(?first_seen) AS ?dt_first_seen )
	BIND(xsd:dateTime(?valid_from) AS ?dt_valid_from )
	

	# Form the IRI for the stixBundle
	BIND(IRI(CONCAT("http://example/ns/", ?id)) AS ?object_iri )
	BIND(IRI(CONCAT("http://example/ns/", ?source_ref)) AS ?source_ref_iri )
	BIND(IRI(CONCAT("http://example/ns/", ?target_ref)) AS ?target_ref_iri )
	
	# bind the appropriate predicate based on relationship_type
	# The binding must be in a nested IF series of statements
	# BIND( IF(?relationship_type = "uses", stix:uses, ?nothing ) AS ?relation_iri )
#	BIND( IF(?relationship_type = "uses", IRI(stix:uses), 
#			IF(?relationship_type = "indicates", IRI(stix:indicates), ?nothing ) ) AS ?relation_iri )
#	BIND( IF(?relationship_type = "indicates", IRI(stix:indicates), 
#			IRI(stix:uses) ) AS ?relation_iri )
	BIND (IRI(CONCAT("http://docs.oasis-open.org/cti/ns/stix#", STR(?relationship_type))) AS ?relation_iri )
	
	
	
	# Form the stix type of either stix:Bundle or stix:StixObject
	BIND ((IF(?type = "bundle", IRI("http://docs.oasis-open.org/cti/ns/stix#Bundle"),  IRI("http://docs.oasis-open.org/cti/ns/stix#StixObject"))) AS ?stixType )
}
